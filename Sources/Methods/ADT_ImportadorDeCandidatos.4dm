//%attributes = {}
  //ADT_ImportadorDeCandidatos

ARRAY TEXT:C222(at_ref_fields;0)
APPEND TO ARRAY:C911(at_ref_fields;String:C10(Table:C252(->[Alumnos:2]))+"."+String:C10(Field:C253(->[Alumnos:2]RUT:5)))
APPEND TO ARRAY:C911(at_ref_fields;String:C10(Table:C252(->[Alumnos:2]))+"."+String:C10(Field:C253(->[Alumnos:2]Apellido_paterno:3)))
APPEND TO ARRAY:C911(at_ref_fields;String:C10(Table:C252(->[Alumnos:2]))+"."+String:C10(Field:C253(->[Alumnos:2]Apellido_materno:4)))
APPEND TO ARRAY:C911(at_ref_fields;String:C10(Table:C252(->[Alumnos:2]))+"."+String:C10(Field:C253(->[Alumnos:2]Nombres:2)))
APPEND TO ARRAY:C911(at_ref_fields;String:C10(Table:C252(->[Alumnos:2]))+"."+String:C10(Field:C253(->[Alumnos:2]Sexo:49)))
APPEND TO ARRAY:C911(at_ref_fields;String:C10(Table:C252(->[Alumnos:2]))+"."+String:C10(Field:C253(->[Alumnos:2]Fecha_de_nacimiento:7)))
APPEND TO ARRAY:C911(at_ref_fields;String:C10(Table:C252(->[Alumnos:2]))+"."+String:C10(Field:C253(->[Alumnos:2]Direccion:12)))
APPEND TO ARRAY:C911(at_ref_fields;String:C10(Table:C252(->[Alumnos:2]))+"."+String:C10(Field:C253(->[Alumnos:2]Codigo_Postal:13)))
APPEND TO ARRAY:C911(at_ref_fields;String:C10(Table:C252(->[Alumnos:2]))+"."+String:C10(Field:C253(->[Alumnos:2]Comuna:14)))
APPEND TO ARRAY:C911(at_ref_fields;String:C10(Table:C252(->[Alumnos:2]))+"."+String:C10(Field:C253(->[Alumnos:2]Región_o_estado:16)))
APPEND TO ARRAY:C911(at_ref_fields;String:C10(Table:C252(->[ADT_Candidatos:49]))+"."+String:C10(Field:C253(->[ADT_Candidatos:49]Entrevistador:20)))
APPEND TO ARRAY:C911(at_ref_fields;String:C10(Table:C252(->[ADT_Candidatos:49]))+"."+String:C10(Field:C253(->[ADT_Candidatos:49]Postula_a:6)))
APPEND TO ARRAY:C911(at_ref_fields;String:C10(Table:C252(->[ADT_Candidatos:49]))+"."+String:C10(Field:C253(->[ADT_Candidatos:49]Como_Llego_al_Colegio:47)))
APPEND TO ARRAY:C911(at_ref_fields;String:C10(Table:C252(->[ADT_Candidatos:49]))+"."+String:C10(Field:C253(->[ADT_Candidatos:49]Estado:52)))
APPEND TO ARRAY:C911(at_ref_fields;String:C10(Table:C252(->[ADT_Candidatos:49]))+"."+String:C10(Field:C253(->[ADT_Candidatos:49]Observaciones_entrevista:12)))
APPEND TO ARRAY:C911(at_ref_fields;String:C10(Table:C252(->[ADT_Candidatos_Observaciones:154]))+"."+String:C10(Field:C253(->[ADT_Candidatos_Observaciones:154]Observaciones:2)))
APPEND TO ARRAY:C911(at_ref_fields;String:C10(Table:C252(->[STR_EducacionAnterior:87]))+"."+String:C10(Field:C253(->[STR_EducacionAnterior:87]Nombre_Colegio:1)))
APPEND TO ARRAY:C911(at_ref_fields;String:C10(Table:C252(->[STR_EducacionAnterior:87]))+"."+String:C10(Field:C253(->[STR_EducacionAnterior:87]País:2)))
APPEND TO ARRAY:C911(at_ref_fields;String:C10(Table:C252(->[STR_EducacionAnterior:87]))+"."+String:C10(Field:C253(->[STR_EducacionAnterior:87]Nivel:3)))
APPEND TO ARRAY:C911(at_ref_fields;String:C10(Table:C252(->[STR_EducacionAnterior:87]))+"."+String:C10(Field:C253(->[STR_EducacionAnterior:87]Año:4)))
APPEND TO ARRAY:C911(at_ref_fields;String:C10(Table:C252(->[Personas:7]))+"."+String:C10(Field:C253(->[Personas:7]RUT:6)))
APPEND TO ARRAY:C911(at_ref_fields;String:C10(Table:C252(->[Personas:7]))+"."+String:C10(Field:C253(->[Personas:7]Apellido_paterno:3)))
APPEND TO ARRAY:C911(at_ref_fields;String:C10(Table:C252(->[Personas:7]))+"."+String:C10(Field:C253(->[Personas:7]Apellido_materno:4)))
APPEND TO ARRAY:C911(at_ref_fields;String:C10(Table:C252(->[Personas:7]))+"."+String:C10(Field:C253(->[Personas:7]Nombres:2)))
APPEND TO ARRAY:C911(at_ref_fields;String:C10(Table:C252(->[Personas:7]))+"."+String:C10(Field:C253(->[Personas:7]Direccion:14)))
APPEND TO ARRAY:C911(at_ref_fields;String:C10(Table:C252(->[Personas:7]))+"."+String:C10(Field:C253(->[Personas:7]eMail:34)))
APPEND TO ARRAY:C911(at_ref_fields;String:C10(Table:C252(->[Personas:7]))+"."+String:C10(Field:C253(->[Personas:7]Celular:24)))
APPEND TO ARRAY:C911(at_ref_fields;String:C10(Table:C252(->[Personas:7]))+"."+String:C10(Field:C253(->[Personas:7]RUT:6)))
APPEND TO ARRAY:C911(at_ref_fields;String:C10(Table:C252(->[Personas:7]))+"."+String:C10(Field:C253(->[Personas:7]Apellido_paterno:3)))
APPEND TO ARRAY:C911(at_ref_fields;String:C10(Table:C252(->[Personas:7]))+"."+String:C10(Field:C253(->[Personas:7]Apellido_materno:4)))
APPEND TO ARRAY:C911(at_ref_fields;String:C10(Table:C252(->[Personas:7]))+"."+String:C10(Field:C253(->[Personas:7]Nombres:2)))
APPEND TO ARRAY:C911(at_ref_fields;String:C10(Table:C252(->[Personas:7]))+"."+String:C10(Field:C253(->[Personas:7]Direccion:14)))
APPEND TO ARRAY:C911(at_ref_fields;String:C10(Table:C252(->[Personas:7]))+"."+String:C10(Field:C253(->[Personas:7]eMail:34)))
APPEND TO ARRAY:C911(at_ref_fields;String:C10(Table:C252(->[Personas:7]))+"."+String:C10(Field:C253(->[Personas:7]Celular:24)))

ARRAY TEXT:C222(at_ref_tables;0)
APPEND TO ARRAY:C911(at_ref_tables;String:C10(Table:C252(->[Alumnos:2])))
APPEND TO ARRAY:C911(at_ref_tables;String:C10(Table:C252(->[ADT_Candidatos:49])))
APPEND TO ARRAY:C911(at_ref_tables;String:C10(Table:C252(->[ADT_Candidatos_Observaciones:154])))
APPEND TO ARRAY:C911(at_ref_tables;String:C10(Table:C252(->[STR_EducacionAnterior:87])))
APPEND TO ARRAY:C911(at_ref_tables;String:C10(Table:C252(->[Personas:7])))

ST_LoadModuleFormatExceptions 
C_POINTER:C301($ptr_table)
C_TIME:C306($ref)

$ref:=Open document:C264("";"";Read mode:K24:5)

If ($ref#?00:00:00?)
	
	ARRAY TEXT:C222($at_header;0)
	C_TEXT:C284($header;$linea;$campo_propio;$tipo_cp)
	C_LONGINT:C283($hl_Estados)
	$hl_Estados:=New list:C375
	$hl_Estados:=ADTcfg_LoadEstados 
	HL_ExpandAll ($hl_Estados)
	
	If (SYS_IsWindows )
		USE CHARACTER SET:C205("windows-1252";1)
	Else 
		USE CHARACTER SET:C205("MacRoman";1)
	End if 
	
	$l_ProgressProcID:=IT_Progress (1;0;$l_ProgressProcID;__ ("Importando archivo con candidatos ..."))
	
	RECEIVE PACKET:C104($ref;$header;"\r")
	AT_Text2Array (->$at_header;$header;"\t")
	
	  //CAMPOS PROPIOS
	If (Size of array:C274($at_header)>Size of array:C274(at_ref_tables))
		
		ARRAY TEXT:C222($at_CP;0)
		ARRAY TEXT:C222($at_CP_code;0)
		ARRAY TEXT:C222($at_CP_target;0)
		
		ARRAY POINTER:C280($ap_CP_table;0)
		ARRAY POINTER:C280($ap_field_id;0)
		ARRAY POINTER:C280($ap_cp_subT;0)
		ARRAY POINTER:C280($ap_cp_subT_field;0)
		ARRAY BOOLEAN:C223($ab_multi_eva;0)
		
		For ($i;Size of array:C274(at_ref_fields)+1;Size of array:C274($at_header))
			$at_header{$i}:=ST_GetCleanString ($at_header{$i})
			$pos:=Position:C15("(";$at_header{$i})
			$pos2:=Position:C15(")";$at_header{$i})
			$tipo_cp:=Substring:C12($at_header{$i};1;$pos-1)
			$campo_propio:=Substring:C12($at_header{$i};$pos+1;($pos2-$pos)-1)
			$modulo:=""
			
			Case of 
				: ($tipo_cp="CP-Candidato")
					$ptr_table:=->[ADT_Candidatos:49]
					$modulo:="AdmissionTrack"
					$ptr_field:=->[ADT_Candidatos:49]Candidato_numero:1
					$ptr_subT:=->[ADT_Candidatos:49]UserFields:42
					$ptr_subT_field:=->[ADT_Candidatos]UserFields'Value
					APPEND TO ARRAY:C911($at_CP_target;"Candidato")
					
				: (($tipo_cp="CP-Padre@") | ($tipo_cp="CP-Madre@"))
					
					If ($tipo_cp="CP-Padre@")
						APPEND TO ARRAY:C911($at_CP_target;"Padre")
					Else 
						APPEND TO ARRAY:C911($at_CP_target;"Madre")
					End if 
					
					$ptr_table:=->[Personas:7]
					$modulo:="SchoolTrack"
					$ptr_field:=->[Personas:7]No:1
					$ptr_subT:=->[Personas:7]Userfields:31
					$ptr_subT_field:=->[Personas]Userfields'Value
					
			End case 
			
			If ($modulo#"")
				UFLD_LoadFileTplt ($ptr_table;$modulo)
				$fia:=Find in array:C230(aUFList;$campo_propio)
				
				If ($fia=-1)
					READ WRITE:C146([xShell_Userfields:76])
					CREATE RECORD:C68([xShell_Userfields:76])
					[xShell_Userfields:76]FieldID:7:=SQ_SeqNumber (->[xShell_Userfields:76]FieldID:7)
					[xShell_Userfields:76]UserFieldName:1:=$campo_propio
					[xShell_Userfields:76]FileNo:6:=Table:C252($ptr_table)
					[xShell_Userfields:76]ModuleName:10:=$modulo
					[xShell_Userfields:76]MultiEvaluado:8:=False:C215  //por ahora así, pero también podría venir especificado en el nombre de la columna
					SAVE RECORD:C53([xShell_Userfields:76])
					$cp_code:=String:C10([xShell_Userfields:76]FieldID:7;"00000")+"/"
					APPEND TO ARRAY:C911($ab_multi_eva;[xShell_Userfields:76]MultiEvaluado:8)
					KRL_UnloadReadOnly (->[xShell_Userfields:76])
				Else 
					APPEND TO ARRAY:C911($ab_multi_eva;aUFMulti{$fia})
					$cp_code:=String:C10(aUFID{$fia};"00000")+"/"
				End if 
				
				APPEND TO ARRAY:C911($at_CP;$campo_propio)
				APPEND TO ARRAY:C911($at_CP_code;$cp_code)
				
				APPEND TO ARRAY:C911($ap_field_id;$ptr_field)
				APPEND TO ARRAY:C911($ap_CP_table;$ptr_table)
				APPEND TO ARRAY:C911($ap_cp_subT;$ptr_subT)
				APPEND TO ARRAY:C911($ap_cp_subT_field;$ptr_subT_field)
				
			End if 
			
		End for 
		
	End if 
	
	  //IMPORTACION
	$linea:=""
	RECEIVE PACKET:C104($ref;$linea;"\r")
	ARRAY LONGINT:C221($al_id_alumnos_creados;0)
	ARRAY TEXT:C222($at_rut_alumnos_creados;0)
	
	While ($linea#"")
		ARRAY TEXT:C222($at_linea;0)
		ARRAY TEXT:C222($at_linea;Size of array:C274($at_header))
		AT_Text2Array (->$at_linea;$linea;"\t")
		$id_padre:=0
		$id_madre:=0
		$id_alumno:=0
		
		
		  //JVP 20160801 verifico que el arreglo a importar tenga la misma cantidad de columnas que el header
		  //en caso de no tener el campo deberia venir con el espacio para lo tome como posicion
		If (Size of array:C274($at_linea)=Size of array:C274($at_header))
			
			For ($i;1;Size of array:C274(at_ref_tables))
				
				ARRAY LONGINT:C221($DA_Return;0)
				at_ref_fields{0}:=at_ref_tables{$i}+"."
				AT_SearchArray (->at_ref_fields;">>";->$DA_Return)
				
				Case of 
					: (at_ref_tables{$i}="2")  //alumnos
						$identificador_alu:=""
						$vb_save:=False:C215
						START TRANSACTION:C239
						READ WRITE:C146([Alumnos:2])
						
						For ($x;1;Size of array:C274($DA_Return))
							
							$pos:=Position:C15(".";at_ref_fields{$DA_Return{$x}})
							$num_table:=Num:C11(Substring:C12(at_ref_fields{$DA_Return{$x}};1;$pos-1))
							$num_field:=Num:C11(Substring:C12(at_ref_fields{$DA_Return{$x}};$pos+1))
							$ptr_field:=Field:C253($num_table;$num_field)
							
							Case of 
								: (at_ref_fields{$DA_Return{$x}}="2.5")  //[Alumnos]RUT
									
									If ($at_linea{$DA_Return{$x}}#"")
										
										$rn_alu:=Find in field:C653([Alumnos:2]RUT:5;$at_linea{$DA_Return{$x}})
										$identificador_alu:=$at_linea{$DA_Return{$x}}
										
										If ($rn_alu=-1)
											CREATE RECORD:C68([Alumnos:2])
											[Alumnos:2]numero:1:=SQ_SeqNumber (->[Alumnos:2]numero:1)
											[Alumnos:2]RUT:5:=$at_linea{$DA_Return{$x}}
											[Alumnos:2]nivel_numero:29:=Nivel_AdmissionTrack
											[Alumnos:2]Nivel_Nombre:34:="AdmissionTrack"
											[Alumnos:2]curso:20:="POST"
											[Alumnos:2]Fecha_de_Creacion:21:=Current date:C33(*)
											[Alumnos:2]Fecha_de_modificacion:22:=Current date:C33(*)
											[Alumnos:2]Modificado_por:23:=""
											$id_alumno:=[Alumnos:2]numero:1
											$vb_save:=True:C214
										Else 
											$x:=Size of array:C274($DA_Return)
										End if 
										
									Else 
										$x:=Size of array:C274($DA_Return)
									End if 
									
								: ((at_ref_fields{$DA_Return{$x}}="2.3") | (at_ref_fields{$DA_Return{$x}}="2.4") | (at_ref_fields{$DA_Return{$x}}="2.2"))  //[Alumnos]Apellido_paterno - [Alumnos]Apellido_materno - [Alumnos]Apellidos_y_Nombres
									
									If ($at_linea{$DA_Return{$x}}#"")
										ST_Text2Anything ($ptr_field;$at_linea{$DA_Return{$x}})
									Else 
										$vb_save:=False:C215
										$x:=Size of array:C274($DA_Return)
									End if 
								Else 
									ST_Text2Anything ($ptr_field;$at_linea{$DA_Return{$x}})
							End case 
							
						End for 
						
						If ($vb_save)
							AL_ProcesaNombres (False:C215)
							
							SAVE RECORD:C53([Alumnos:2])
							READ WRITE:C146([ADT_Candidatos:49])
							CREATE RECORD:C68([ADT_Candidatos:49])
							[ADT_Candidatos:49]Candidato_numero:1:=[Alumnos:2]numero:1
							[ADT_Candidatos:49]Fecha_de_Inscripción:2:=Current date:C33(*)
							[ADT_Candidatos:49]RUT:46:=[Alumnos:2]RUT:5
							[ADT_Candidatos:49]Sexo:25:=[Alumnos:2]Sexo:49
							SAVE RECORD:C53([ADT_Candidatos:49])
							KRL_UnloadReadOnly (->[ADT_Candidatos:49])
							VALIDATE TRANSACTION:C240
							APPEND TO ARRAY:C911($al_id_alumnos_creados;$id_alumno)
							APPEND TO ARRAY:C911($at_rut_alumnos_creados;$identificador_alu)
						Else 
							$id_alumno:=0
							CANCEL TRANSACTION:C241
						End if 
						
						KRL_UnloadReadOnly (->[Alumnos:2])
						
					: (at_ref_tables{$i}="49")  //datos candidato
						
						If ($vb_save)
							READ WRITE:C146([ADT_Candidatos:49])
							QUERY:C277([ADT_Candidatos:49];[ADT_Candidatos:49]Candidato_numero:1=$id_alumno)
							
							For ($x;1;Size of array:C274($DA_Return))
								$pos:=Position:C15(".";at_ref_fields{$DA_Return{$x}})
								$num_table:=Num:C11(Substring:C12(at_ref_fields{$DA_Return{$x}};1;$pos-1))
								$num_field:=Num:C11(Substring:C12(at_ref_fields{$DA_Return{$x}};$pos+1))
								$ptr_field:=Field:C253($num_table;$num_field)
								
								ST_Text2Anything ($ptr_field;$at_linea{$DA_Return{$x}})
								Case of 
									: ($num_field=6)  //postula_a
										[ADT_Candidatos:49]Postula_a_Nombre:41:=KRL_GetTextFieldData (->[xxSTR_Niveles:6]NoNivel:5;->[ADT_Candidatos:49]Postula_a:6;->[xxSTR_Niveles:6]Nivel:1)
									: ($num_field=52)  // estado
										ARRAY LONGINT:C221($array_ref;0)
										$vl_pos:=Find in list:C952($hl_Estados;$ptr_field->;0;$array_ref)
										GET LIST ITEM:C378($hl_Estados;$vl_pos;$referencia;$text)
										[ADT_Candidatos:49]ID_Estado:49:=$referencia
								End case 
								
							End for 
							
							SAVE RECORD:C53([ADT_Candidatos:49])
							KRL_UnloadReadOnly (->[ADT_Candidatos:49])
						End if 
						
					: (at_ref_tables{$i}="154")  //observaciones candidatos
						$fia:=Find in array:C230($at_rut_alumnos_creados;$identificador_alu)
						For ($x;1;Size of array:C274($DA_Return))
							If (($at_linea{$DA_Return{$x}}#"") & (Size of array:C274($al_id_alumnos_creados)>0) & ($fia>0))
								
								READ WRITE:C146([ADT_Candidatos_Observaciones:154])
								CREATE RECORD:C68([ADT_Candidatos_Observaciones:154])
								[ADT_Candidatos_Observaciones:154]IngresadaPor:4:=<>tUSR_CurrentUser
								[ADT_Candidatos_Observaciones:154]ID_Observacion:1:=SQ_SeqNumber (->[ADT_Candidatos_Observaciones:154]ID_Observacion:1)
								[ADT_Candidatos_Observaciones:154]FechaIngreso:3:=Current date:C33(*)
								[ADT_Candidatos_Observaciones:154]IDAlumno:5:=$al_id_alumnos_creados{$fia}
								[ADT_Candidatos_Observaciones:154]Observaciones:2:=$at_linea{$DA_Return{$x}}
								SAVE RECORD:C53([ADT_Candidatos_Observaciones:154])
								KRL_UnloadReadOnly (->[ADT_Candidatos_Observaciones:154])
								
							End if 
						End for 
						
					: (at_ref_tables{$i}="87")  //educación anterior
						
						If (Size of array:C274($al_id_alumnos_creados)>0)
							
							For ($x;1;Size of array:C274($DA_Return))
								
								$pos:=Position:C15(".";at_ref_fields{$DA_Return{$x}})
								$num_table:=Num:C11(Substring:C12(at_ref_fields{$DA_Return{$x}};1;$pos-1))
								$num_field:=Num:C11(Substring:C12(at_ref_fields{$DA_Return{$x}};$pos+1))
								$ptr_field:=Field:C253($num_table;$num_field)
								
								If ($x=1)
									START TRANSACTION:C239
									CREATE RECORD:C68([STR_EducacionAnterior:87])
									[STR_EducacionAnterior:87]ID_EducacionAnterior:9:=SQ_SeqNumber (->[STR_EducacionAnterior:87]ID_EducacionAnterior:9)
									[STR_EducacionAnterior:87]ID_Alumno:5:=$al_id_alumnos_creados{Size of array:C274($al_id_alumnos_creados)}
									[STR_EducacionAnterior:87]Tipo_Persona:8:="al"
									ST_Text2Anything ($ptr_field;$at_linea{$DA_Return{$x}})
								Else 
									ST_Text2Anything ($ptr_field;$at_linea{$DA_Return{$x}})
								End if 
							End for 
							
							If (([STR_EducacionAnterior:87]Año:4=0) & ([STR_EducacionAnterior:87]Nivel:3="") & ([STR_EducacionAnterior:87]País:2="") & ([STR_EducacionAnterior:87]Nombre_Colegio:1=""))
								CANCEL TRANSACTION:C241
							Else 
								SAVE RECORD:C53([STR_EducacionAnterior:87])
								VALIDATE TRANSACTION:C240
							End if 
							KRL_UnloadReadOnly (->[STR_EducacionAnterior:87])
						End if 
						
						
					: (at_ref_tables{$i}="7")  //Personas
						
						$vb_save:=False:C215
						
						For ($x;1;Size of array:C274($DA_Return))
							
							$pos:=Position:C15(".";at_ref_fields{$DA_Return{$x}})
							$num_table:=Num:C11(Substring:C12(at_ref_fields{$DA_Return{$x}};1;$pos-1))
							$num_field:=Num:C11(Substring:C12(at_ref_fields{$DA_Return{$x}};$pos+1))
							$ptr_field:=Field:C253($num_table;$num_field)
							
							Case of 
								: ($num_field=6)
									If ($at_linea{$DA_Return{$x}}#"")
										START TRANSACTION:C239
										READ WRITE:C146([Personas:7])
										$rn_per:=Find in field:C653([Personas:7]RUT:6;$at_linea{$DA_Return{$x}})
										
										If ($rn_per=-1)
											CREATE RECORD:C68([Personas:7])
											[Personas:7]No:1:=SQ_SeqNumber (->[Personas:7]No:1)
											$vb_save:=True:C214
											[Personas:7]RUT:6:=$at_linea{$DA_Return{$x}}
											If ($x=1)
												[Personas:7]Sexo:8:="M"
											Else 
												[Personas:7]Sexo:8:="F"
											End if 
										Else 
											GOTO RECORD:C242([Personas:7];$rn_per)
										End if 
										
										If ($x=1)
											$id_padre:=[Personas:7]No:1
										Else 
											$id_madre:=[Personas:7]No:1
										End if 
										
									End if 
									
								: ($num_field=24)
									ST_Text2Anything ($ptr_field;$at_linea{$DA_Return{$x}})
									
									If ($vb_save)
										[Personas:7]Apellidos_y_nombres:30:=Replace string:C233([Personas:7]Apellido_paterno:3+" "+[Personas:7]Apellido_materno:4+" "+[Personas:7]Nombres:2;"  ";" ")
										[Personas:7]Apellidos_y_nombres:30:=ST_Format (->[Personas:7]Apellidos_y_nombres:30)
										[Personas:7]Nombre_Comun:60:=ST_GetWord ([Personas:7]Nombres:2;1)+" "+[Personas:7]Apellido_paterno:3
										SAVE RECORD:C53([Personas:7])
										VALIDATE TRANSACTION:C240
									Else 
										CANCEL TRANSACTION:C241
									End if 
									
								Else 
									ST_Text2Anything ($ptr_field;$at_linea{$DA_Return{$x}})
							End case 
						End for 
						KRL_UnloadReadOnly (->[Personas:7])
				End case 
				
			End for 
			
			  //VERIFICACIÓN Y CREACIÓN DE FAMILIA
			If ($id_alumno#0)
				$nombre familia:=""
				
				If (($id_padre#0) & ($id_madre#0))
					$nombre familia:=KRL_GetTextFieldData (->[Personas:7]No:1;->$id_padre;->[Personas:7]Apellido_paterno:3)+" "+KRL_GetTextFieldData (->[Personas:7]No:1;->$id_madre;->[Personas:7]Apellido_paterno:3)
				Else 
					$nombre familia:=KRL_GetTextFieldData (->[Alumnos:2]numero:1;->$id_alumno;->[Alumnos:2]Apellido_paterno:3)+" "+KRL_GetTextFieldData (->[Alumnos:2]numero:1;->$id_alumno;->[Alumnos:2]Apellido_materno:4)
				End if 
				
				READ ONLY:C145([Familia:78])
				QUERY:C277([Familia:78];[Familia:78]Nombre_de_la_familia:3=$nombre familia;*)
				QUERY:C277([Familia:78]; & ;[Familia:78]Padre_Número:5=$id_padre;*)
				QUERY:C277([Familia:78]; & ;[Familia:78]Madre_Número:6=$id_madre)
				
				$id_familia:=0
				  //$rn_familia:=Find in field([Familia]Nombre_de_la_familia;$nombre familia)
				
				If (Records in selection:C76([Familia:78])=0)
					READ WRITE:C146([Familia:78])
					CREATE RECORD:C68([Familia:78])
					[Familia:78]Numero:1:=SQ_SeqNumber (->[Familia:78]Numero:1)
					[Familia:78]Nombre_de_la_familia:3:=$nombre familia
					[Familia:78]Es_Postulante:18:=True:C214
					[Familia:78]Madre_Número:6:=$id_madre
					[Familia:78]Madre_Nombre:16:=KRL_GetTextFieldData (->[Personas:7]No:1;->$id_madre;->[Personas:7]Apellidos_y_nombres:30)
					[Familia:78]Padre_Número:5:=$id_padre
					[Familia:78]Padre_Nombre:15:=KRL_GetTextFieldData (->[Personas:7]No:1;->$id_padre;->[Personas:7]Apellidos_y_nombres:30)
					SAVE RECORD:C53([Familia:78])
					$id_familia:=[Familia:78]Numero:1
					KRL_UnloadReadOnly (->[Familia:78])
				Else 
					$id_familia:=[Familia:78]Numero:1
				End if 
				
				READ WRITE:C146([Familia_RelacionesFamiliares:77])
				
				If (($id_padre#0) & ($id_familia#0))
					REDUCE SELECTION:C351([Familia_RelacionesFamiliares:77];0)
					QUERY:C277([Familia_RelacionesFamiliares:77];[Familia_RelacionesFamiliares:77]ID_Familia:2=$id_familia;*)
					QUERY:C277([Familia_RelacionesFamiliares:77]; & ;[Familia_RelacionesFamiliares:77]ID_Persona:3=$id_padre)
					If (Records in selection:C76([Familia_RelacionesFamiliares:77])=0)
						CREATE RECORD:C68([Familia_RelacionesFamiliares:77])
						[Familia_RelacionesFamiliares:77]ID_Familia:2:=$id_familia
						[Familia_RelacionesFamiliares:77]Tipo_Relación:4:=2
						[Familia_RelacionesFamiliares:77]ID_Persona:3:=$id_padre
						[Familia_RelacionesFamiliares:77]Parentesco:6:=__ ("Padre")
						SAVE RECORD:C53([Familia_RelacionesFamiliares:77])
					End if 
				End if 
				
				If (($id_madre#0) & ($id_familia#0))
					REDUCE SELECTION:C351([Familia_RelacionesFamiliares:77];0)
					QUERY:C277([Familia_RelacionesFamiliares:77];[Familia_RelacionesFamiliares:77]ID_Familia:2=$id_familia;*)
					QUERY:C277([Familia_RelacionesFamiliares:77]; & ;[Familia_RelacionesFamiliares:77]ID_Persona:3=$id_madre)
					If (Records in selection:C76([Familia_RelacionesFamiliares:77])=0)
						CREATE RECORD:C68([Familia_RelacionesFamiliares:77])
						[Familia_RelacionesFamiliares:77]ID_Familia:2:=$id_familia
						[Familia_RelacionesFamiliares:77]Tipo_Relación:4:=1
						[Familia_RelacionesFamiliares:77]ID_Persona:3:=$id_madre
						[Familia_RelacionesFamiliares:77]Parentesco:6:=__ ("Madre")
						SAVE RECORD:C53([Familia_RelacionesFamiliares:77])
					End if 
				End if 
				
				CREATE RECORD:C68([Familia_RelacionesFamiliares:77])
				[Familia_RelacionesFamiliares:77]ID_Familia:2:=$id_familia
				[Familia_RelacionesFamiliares:77]ID_Alumno:1:=$id_alumno
				SAVE RECORD:C53([Familia_RelacionesFamiliares:77])
				
				READ WRITE:C146([Alumnos:2])
				QUERY:C277([Alumnos:2];[Alumnos:2]numero:1=$id_alumno)
				[Alumnos:2]Familia_Número:24:=$id_familia
				SAVE RECORD:C53([Alumnos:2])
				KRL_UnloadReadOnly (->[Alumnos:2])
				
				READ WRITE:C146([ADT_Candidatos:49])
				QUERY:C277([ADT_Candidatos:49];[ADT_Candidatos:49]Candidato_numero:1=$id_alumno)
				[ADT_Candidatos:49]Familia_numero:30:=$id_familia
				[ADT_Candidatos:49]Padre_numero:31:=$id_padre
				[ADT_Candidatos:49]Padre_nombre:33:=KRL_GetTextFieldData (->[Personas:7]No:1;->$id_padre;->[Personas:7]Apellidos_y_nombres:30)
				[ADT_Candidatos:49]Madre_numero:32:=$id_madre
				[ADT_Candidatos:49]Madre_nombre:34:=KRL_GetTextFieldData (->[Personas:7]No:1;->$id_madre;->[Personas:7]Apellidos_y_nombres:30)
				SAVE RECORD:C53([ADT_Candidatos:49])
				
				KRL_UnloadReadOnly (->[ADT_Candidatos:49])
				KRL_UnloadReadOnly (->[Familia_RelacionesFamiliares:77])
				
			End if 
			
			  //CAMPOR PROPIOS
			For ($i;1;Size of array:C274($at_CP))
				
				Case of 
					: ($ap_CP_table{$i}=Table:C252(49))  //candidatos
						$id:=$id_alumno
					: ($ap_CP_table{$i}=Table:C252(7))  //personas
						
						If ($at_CP_target{$i}="Madre")
							$id:=$id_madre
						Else 
							$id:=$id_padre
						End if 
						
				End case 
				
				If ($id>0)
					READ WRITE:C146($ap_CP_table{$i}->)
					QUERY:C277($ap_CP_table{$i}->;$ap_field_id{$i}->=$id)
					_O_QUERY SUBRECORDS:C108($ap_cp_subT{$i}->;$ap_cp_subT_field{$i}->=($at_CP_code{$i}+"@"))
					
					If (_O_Records in subselection:C7($ap_cp_subT{$i}->)>0)
						If ($ab_multi_eva{$i})
							_O_CREATE SUBRECORD:C72($ap_cp_subT{$i}->)
						End if 
						$ap_cp_subT_field{$i}->:=$at_CP_code{$i}+$at_linea{Size of array:C274(at_ref_fields)+$i}
					Else 
						_O_CREATE SUBRECORD:C72($ap_cp_subT{$i}->)
						$ap_cp_subT_field{$i}->:=$at_CP_code{$i}+$at_linea{Size of array:C274(at_ref_fields)+$i}
					End if 
					SAVE RECORD:C53($ap_CP_table{$i}->)
					KRL_UnloadReadOnly ($ap_CP_table{$i})
				End if 
				
			End for 
			
		End if 
		
		
		
		
		$l_ProgressProcID:=IT_Progress (0;$l_ProgressProcID;Get document position:C481($ref)/Get document size:C479($ref))
		
		RECEIVE PACKET:C104($ref;$linea;"\r")
	End while 
	$l_ProgressProcID:=IT_Progress (-1;$l_ProgressProcID)
	
	CLOSE DOCUMENT:C267($ref)
	USE CHARACTER SET:C205(*;1)
	
	CD_Dlog (0;__ ("Fin de la importación de candidatos"))
End if 


